import org.apache.commons.io.Charsets
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java' // java

    // for shading in dependencies
    id "com.github.johnrengelman.shadow" version "7.1.2"

    // for minecraft lol
    id ("io.papermc.paperweight.userdev") version "1.3.5"
}

// Properties
group   rootProject.group
version rootProject.version

java {
    // Configure the java toolchain. This allows gradle to auto-provision JDK 17 on systems that only have JDK 8 installed for example.
    toolchain.languageVersion.set(JavaLanguageVersion.of(17))
}

// Javadocs
javadoc {
    source = sourceSets.main.allJava
    destinationDir new File("./docs/javadocs/")
}

// Task Configuration

tasks {

    assemble {
        dependsOn(shadowJar, reobfJar) }

    compileJava {
        options.encoding = Charsets.UTF_8.name()
        options.release.set(17) }
    javadoc {
        options.encoding = Charsets.UTF_8.name() }
    processResources {
        filteringCharset = Charsets.UTF_8.name() }


}

processResources {

    // replace placeholders for epicness
    // (and streamlining) but mostly epicness
    // (and other processing)
    filesNotMatching([
            // media
            '**/*.png',
            '**/*.ogg',
            '**/*.wav',
            '**/*.mp3',

            // compiled
            '**/*.jar',
            '**/*.class'
    ] as Iterable<String>) {
        filter ReplaceTokens, tokens: [
                carbonProjectVersion: project.version,
        ]
    }

}

// Dependencies

repositories {
    mavenCentral()
    mavenLocal()

    // tried to use mixins lol
    maven { url = "https://repo.spongepowered.org/repository/maven-public/" }

    maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
}

dependencies {

    // Minecraft
    paperweightDevelopmentBundle("io.papermc.paper:dev-bundle:1.18.2-R0.1-SNAPSHOT")

    //  Modules
    implementation project(":carbon-util")
    implementation project(":carbon-util-message")

    // External Libraries

}

shadowJar {
    // relocate BStats into the right directory
    relocate 'org.bstats', 'com.github.orbyfied.carbon.vendor.bstats'
}